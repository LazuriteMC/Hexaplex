plugins {
    id 'fabric-loom' version "$loom_version"
}

group 'dev.lazurite'
version hexaplex_version

repositories {
    maven {
        name = 'Ladysnake'
        url = 'https://ladysnake.jfrog.io/artifactory/mods'
        content {
            includeGroup 'io.github.ladysnake'
            includeGroupByRegex 'io\\.github\\.onyxstudios.*'
        }
    }

    maven {
        name 'shedaniel'
        url 'https://maven.shedaniel.me'
    }

    maven {
        name 'TerraformersMC'
        url 'https://maven.terraformersmc.com/releases'
    }
}

dependencies {
    mappings loom.officialMojangMappings()
    minecraft "com.mojang:minecraft:$minecraft_version"
    modImplementation "net.fabricmc:fabric-loader:$fabric_loader_version"

    modImplementation "io.github.ladysnake:satin:$satin_version"
    include "io.github.ladysnake:satin:$satin_version"

    modImplementation "me.shedaniel.cloth:cloth-config-fabric:$cloth_version"

    modImplementation "com.terraformersmc:modmenu:$modmenu_version"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of 17
    withSourcesJar()
}

jar {
    from 'LICENSE'
}

tasks.withType(ProcessResources) {
    inputs.properties(
            'version': project.version,
            'min_minecraft_version': min_minecraft_version,
            'min_fabric_loader_version': min_fabric_loader_version,
            'min_fabric_api_version': min_fabric_api_version
    )

    filesMatching 'fabric.mod.json', {
        expand(
                'version': project.version,
                'min_minecraft_version': min_minecraft_version,
                'min_fabric_loader_version': min_fabric_loader_version,
                'min_fabric_api_version': min_fabric_api_version
        )
    }
}
